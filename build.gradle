import java.awt.Desktop

buildscript {
    repositories {}
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.3'
    id 'org.kordamp.markdown.convert' version '1.2.0'
}

// configures version for release plugin
scmVersion {
    tag {
        prefix = 'tduf'
    }
}

// Helper script
apply from: './src/main/groovy/helper.gradle'

// Project definition
allprojects {
    project.group = 'fr.tduf'

    // version defined by plugin (if changes or ahead commits: x.y.z-SNAPSHOT)
    project.version = scmVersion.version

    // Version for common dependencies
    ext {
        args4jVersion = '2.33'
        
        assertjVersion = '3.15.0'

        commonsLangVersion = '3.10'

        exp4jVersion = '0.4.8'

        j5systemexitVersion = '1.0.0'

        jacksonVersion = '2.10.3'

        jaxbapiVersion = '2.3.1'

        jsonassertVersion = '1.5.0'

        junitJupiterVersion = '5.6.2'

        minlogVersion = '1.3.1'

        mockitoVersion = '3.3.3'

        testfxVersion = '4.0.16-alpha'
    }
}

subprojects {
    apply plugin: 'java'

    repositories {
        maven  {
            url "https://repo1.maven.org/maven2"
        }

        flatDir name: 'embeddedRepository', dirs: '../lib'
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: "$commonsLangVersion"
        implementation group: 'com.esotericsoftware', name: 'minlog', version: "$minlogVersion"

        testImplementation project(':lib-testing')
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$junitJupiterVersion"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$junitJupiterVersion"
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    task fatJar(type: Jar) {
        description 'Generates jar with all dependencies bundled into it.'
        group 'build'
        mustRunAfter 'jar'

        getArchiveBaseName().set('tduf-' + project.name + '-all')
        getArchiveVersion().set('')
        from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}

project(':lib-testing') {
    apply plugin: 'java-library'

    dependencies {
        api group: 'org.assertj', name: 'assertj-core', version: "$assertjVersion"
        api group: 'org.skyscreamer', name: 'jsonassert', version: "$jsonassertVersion"
        api group: 'org.mockito', name: 'mockito-core', version: "$mockitoVersion"
        api group: 'org.testfx', name: 'testfx-junit5', version: "$testfxVersion"
        api group: 'com.ginsberg', name: 'junit5-system-exit', version: "$j5systemexitVersion"

        implementation project(':lib-unlimited')
    }
}

project(':lib-unlimited') {
    apply plugin: 'java-library'

    dependencies {
        implementation group: 'net.objecthunter', name: 'exp4j', version: "$exp4jVersion"
        implementation group: 'javax.xml.bind', name: 'jaxb-api', version: "$jaxbapiVersion"

        api group: 'commons-io', name: 'commons-io', version: '2.6'
    }

    task copyStructures(type: Copy) {
        description 'Copies structures to release directory.'
        group 'packaging'

        from 'src/main/resources/files/structures/'
        include '*'
        into '../pack/structures'
    }
}

project(':cli') {
    dependencies {
        implementation project(':lib-unlimited')
        implementation group: 'args4j', name: 'args4j', version: "$args4jVersion"
    }

    jar {
        manifest.attributes provider: 'gradle'
    }
}

project(':gui-common') {
    apply plugin: 'java-library'

    dependencies {
        api project(':lib-unlimited')

        testImplementation project(':lib-testing')
    }
}

project(':gui-database') {
    dependencies {
        implementation project(':gui-common')
    }

    task execute(type:JavaExec) {
        group 'binaries'

        main 'fr.tduf.gui.database.DatabaseEditor'
        classpath sourceSets.main.runtimeClasspath
    }

    task copyProfiles(type: Copy) {
        description 'Copies all default profiles to release directory.'
        group 'packaging'
        mustRunAfter ':cleanPackDir'

        from 'editorProfiles/'
        into '../pack/editorProfiles/'
    }
}

project(':gui-launcher') {
    dependencies {
        implementation project(':gui-common')
    }

    task execute(type:JavaExec) {
        group 'binaries'

        main 'fr.tduf.gui.launcher.Launcher'
        classpath sourceSets.main.runtimeClasspath
    }
}

project(':gui-installer') {
    apply plugin: 'org.kordamp.markdown.convert'

    dependencies {
        implementation project(':gui-common')
    }

    task execute(type: JavaExec) {
        main 'fr.tduf.gui.installer.Installer'
        classpath sourceSets.main.runtimeClasspath
    }

    task copyDoc(type: Copy) {
        description 'Copies formatted README to release directory.'
        mustRunAfter 'markdownToHtml'

        from "$buildDir/gen-html"
        include '*.html'
        into '../pack/'
    }

    task copyAssets(type: Copy) {
        description 'Copies all assets directory structure to release directory.'
        mustRunAfter ':cleanPackDir'

        from 'assets/'
        into '../pack/assets/'
    }

    task copyTemplates(type: Copy) {
        description 'Copies all patch templates to release directory.'
        mustRunAfter ':cleanPackDir'

        from 'patchTemplates/'
        into '../pack/tools/patchTemplates/'
    }

    task copyMainScripts(type: Copy) {
        description 'Copies CLI main scripts to release directory.'
        mustRunAfter ':cleanPackDir'

        if (project.hasProperty('target') && target == LINUX_TARGET) {
            from 'src/main/shell/linux/'
            include '*.sh'
            include 'linux-aliases'
        } else {
            from 'src/main/shell/windows/'
            include '*.cmd'
        }

        into '../pack/'
    }

    task copyUtilScripts(type: Copy) {
        description 'Copies CLI utility scripts to pack/tools/cli directory.'
        mustRunAfter ':cleanPackDir'

        if (project.hasProperty('target') && target == LINUX_TARGET) {
            from '../src/main/shell/linux/cli-util/'
            include '*.sh'
        } else {
            from '../src/main/shell/windows/cli-util/'
            include '*.cmd'
        }

        into '../pack/tools/cli'
    }

    task copyJar(type: Copy) {
        description 'Copies jar file to release directory.'

        from fatJar
        into '../pack/tools/lib/'
    }

    task updateExternalTools(type: Copy) {
        description 'Copies external tools to installer development tree to ensure proper functioning.'

        from '../tools/'
        into 'tools/'
    }
}

project(':integ-tests') {
    dependencies {
        testImplementation project(':lib-testing')
        testImplementation project(':lib-unlimited')
        testImplementation project(':cli')
    }

    task integTestReport() {
        group 'verification'

        doLast {
            def reportFileAbsolutePath = file('./build/reports/tests/test/index.html').toString().replaceAll('\\\\', '/')
            Desktop.desktop.browse(('file://' + reportFileAbsolutePath).toURI())
        }
    }
    // Unsupported by codeship CI
//    tasks.test.finalizedBy tasks.integTestReport
}

// Main tasks
// Create a list of subprojects to include in the full jar.
def fullProjects = [':lib-unlimited', ':cli', ':gui-common', ':gui-database', ':gui-launcher']
def tempJarDirectory = 'tmp-jarContents/'

task gatherAllContentsForJar(type: Copy, dependsOn: fullProjects.collect{ it+":compileJava"}) {
    description 'Copies contents to temp directory.'
    group 'build'

    // Project sources
    from files(fullProjects.collect{ project(it).sourceSets.main.output })
    // External libraries
    from files(fullProjects.collect{ project(it).configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } } )
    into tempJarDirectory
}

task fullJar( type: Jar , dependsOn: 'gatherAllContentsForJar' ) {
    description 'Creates jar with all contents from temp directory.'
    group 'build'

    getDestinationDirectory().set(file('pack/tools/lib/'))
    getArchiveBaseName().set('tduf')
    from tempJarDirectory
}

task packFull(dependsOn: getPackDependencies(project.hasProperty('target') ? target : WINDOWS_TARGET), type: Zip) {
    description 'Create single release Zip with ALL components except InstallerKit in releases directory.'
    group 'packaging'
    
    def targetName = (project.hasProperty('target') && target == LINUX_TARGET) ? 'linux' : 'windows'
    println("(i) Full packaging for target *$targetName*")
    
    archiveName "tduf-$project.version-${targetName}.zip"
    destinationDir new File('releases/')
    from  'pack/'
}

task packInstallerKit(dependsOn: getInstallerPackDependencies(project.hasProperty('target') ? target : WINDOWS_TARGET), type: Zip) {
    description 'Zips only IK, LIB files from pack folder.'
    group 'packaging'

    def targetName = (project.hasProperty('target') && target == LINUX_TARGET) ? 'linux' : 'windows'
    println("(i) Installer Kit packaging for target *$targetName*")

    archiveName "tduf-IK-$project.version-${targetName}.zip"
    destinationDir new File('releases/')
    from  'pack/'
}

task cleanPackDir(type: Delete) {
    description 'Prepares new package by removing all files in pack directory.'
    group 'packaging'

    delete 'pack/'
}

task cleanTempJarDir(type: Delete) {
    description 'Prepares new package by removing all files in tmp-jarContents directory.'
    group 'packaging'

    delete tempJarDirectory
}

task copyDoc(type: Copy) {
    description 'Copies formatted README to release directory.'
    group 'packaging'
    mustRunAfter 'markdownToHtml'

    from "$buildDir/gen-html"
    include '*.html'
    into 'pack/'
}

task copyMainScripts(type: Copy) {
    description 'Copies CLI main scripts to release directory.'
    group 'packaging'
    mustRunAfter ':cleanPackDir'

    if (project.hasProperty('target') && target == LINUX_TARGET) {
        from 'src/main/shell/linux/'
        include '*.sh'
        include 'linux-aliases'
    } else {
        from 'src/main/shell/windows/'
        include '*.cmd'
    }
    
    into 'pack/'
}

task copyUtilScripts(type: Copy) {
    description 'Copies CLI utility scripts to release/tools/cli directory.'
    group 'packaging'
    mustRunAfter 'cleanPackDir'

    if (project.hasProperty('target') && target == LINUX_TARGET) {
        from 'src/main/shell/linux/cli-util/'
        include '*.sh'
    } else {
        from 'src/main/shell/windows/cli-util/'
        include '*.cmd'
    }
    
    into 'pack/tools/cli'
}

task copyExternalTools(type: Copy) {
    description 'Copies all external tools to release directory.'
    group 'packaging'
    mustRunAfter 'cleanPackDir'

    from 'tools'
    exclude 'readme'
    into 'pack/tools'
}

task copyVersionInfo(type: Copy) {
    description 'Copies version.info file to release libs directory.'
    group 'packaging'
    mustRunAfter 'cleanPackDir'

    from 'dist/'
    include 'version.info'
    into 'pack/tools/lib/'
}

task copyIcons(type: Copy) {
    description 'Copies common GUI icon to packs/icons directory.'
    group 'packaging'
    mustRunAfter 'cleanPackDir'

    from 'gui-common/src/main/resources/gui-common/img/icons'
    include 'TDU-256px.png'
    include 'TDU-256px.ico'
    into 'pack/icons'
}
