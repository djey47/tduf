buildscript {
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '0.9.9'
}

// configure version
scmVersion {
    tag {
        prefix = 'tduf'
    }
}

allprojects {
    project.group = 'fr.tduf'
    project.version = scmVersion.version
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.13'
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2'

        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '1.7.0'
        testCompile group: 'net.sf.json-lib', name: 'json-lib', classifier: 'jdk15', version: '2.4'
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    task fatJar(type: Jar) {
        mustRunAfter 'jar'

        baseName = 'tduf-' + project.name + '-all'
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }
}

project(':cli') {
    dependencies {
        compile project(':lib-unlimited')

        compile group: 'args4j', name: 'args4j', version: '2.0.29'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}