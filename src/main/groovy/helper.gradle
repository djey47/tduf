// Build helpers script

/**
 * Available values for target argument
 */
def PARAM_LINUX_TARGET = 'linux'
def PARAM_WINDOWS_TARGET = 'windows'

// Task dependencies
def fullCleanDeps = [ 'cleanTempJarDir', 'cleanPackDir' ]
def externalDeps = [ 'copyExternalTools', 'copyVersionInfo', 'copyIcons' ]

// List of subprojects to include in the jar.
def fullProjects = [':lib-unlimited', ':cli', ':gui-common', ':gui-database', ':gui-launcher']
def ikProjects = [':lib-unlimited', ':gui-common', ':gui-installer']
def databaseEditorProjects = [':lib-unlimited', ':gui-common', ':gui-database']

/**
 * @returns List of tasks to depend on for FULL packaging, according to target (linux or windows)
 */
def fullPackDependencies = { String target ->
    def commonPackDeps = [ ':gui-database:markdownToHtml', ':gui-launcher:markdownToHtml', 'markdownToHtml',
                           ':gui-database:copyDoc', ':gui-launcher:copyDoc', 'copyDoc',
                           *externalDeps,
                           ':lib-unlimited:copyStructures',
                           ':gui-database:copyProfiles',
                           'fullJar' ]

    def winPackDeps = [  'cli:copyMainScripts', ':gui-database:copyMainScripts', ':gui-launcher:copyMainScripts', 'copySharedScripts', 'copyUtilScripts' ]

    def linuxPackDeps = [ 'cli:copyMainScripts', ':gui-database:copyMainScripts', ':gui-launcher:copyMainScripts', 'copySharedScripts', 'copyUtilScripts' ]

    def isLinuxPackaging = target == PARAM_LINUX_TARGET
    return [ *fullCleanDeps,
             *(isLinuxPackaging ? linuxPackDeps : winPackDeps),
             *commonPackDeps ]
}

/**
 * @returns List of tasks to depend on for IK packaging, according to target (linux or windows)
 */
def installerPackDependencies = { String target ->
    def commonPackDeps = [ ':gui-installer:markdownToHtml',
                           ':gui-installer:copyDoc',
                           *externalDeps,
                           ':gui-installer:copyAssets', ':gui-installer:copyTemplates',
                           'ikJar' ]

    def winPackDeps = [ ':gui-installer:copyMainScripts', ':gui-installer:copyUtilScripts', 'copySharedScripts' ]

    def linuxPackDeps = [ ':gui-installer:copyMainScripts', ':gui-installer:copyUtilScripts', 'copySharedScripts' ]

    def isLinuxPackaging = target == PARAM_LINUX_TARGET
    return [ *fullCleanDeps,
             *(isLinuxPackaging ? linuxPackDeps : winPackDeps),
             *commonPackDeps ]
}

/**
 * @returns List of tasks to depend on for DatabaseEditor packaging, according to target (linux or windows)
 */
def databaseEditorPackDependencies = { String target ->
    def commonPackDeps = [ ':gui-database:markdownToHtml',
                           ':gui-database:copyDoc',
                           *externalDeps,
                           ':gui-database:copyProfiles',
                           'databaseEditorJar' ]

    def winPackDeps = [ ':gui-database:copyMainScripts', ':gui-database:copyUtilScripts', 'copySharedScripts' ]

    def linuxPackDeps = [ ':gui-database:copyMainScripts', ':gui-database:copyUtilScripts', 'copySharedScripts' ]

    def isLinuxPackaging = target == PARAM_LINUX_TARGET
    return [ *fullCleanDeps,
             *(isLinuxPackaging ? linuxPackDeps : winPackDeps),
             *commonPackDeps ]
}

ext {
    getFullProjects = { -> fullProjects }
    getIKProjects = { -> ikProjects }
    getDatabaseEditorProjects = { -> databaseEditorProjects }
    getFullDependencies = fullPackDependencies
    getInstallerDependencies = installerPackDependencies
    getDatabaseEditorDependencies = databaseEditorPackDependencies
    LINUX_TARGET = PARAM_LINUX_TARGET
    WINDOWS_TARGET = PARAM_WINDOWS_TARGET
}
